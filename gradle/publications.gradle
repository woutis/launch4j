apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                afterEvaluate {
                    name = project.description
                    description = project.description
                }
                url = "https://github.com/woutis/launch4j"
                organization {
                    name = "Woutis"
                    url = "https://woutis.com"
                }
                licenses {
                    license {
                        name = "Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE.txt-2.0"
                        distribution = "repo"
                    }
                }
                scm {
                    url = "https://github.com/woutis/launch4j"
                }
                developers {
                    developer {
                        id = "kweny"
                        name = "Kweny"
                        email = "iamkweny@gmail.com"
                    }
                }
                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/woutis/launch4j/issues"
                }
            }
            versionMapping {
                usage('java-api') {
                    fromResolutionResult()
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('gradle-local.properties').newDataInputStream())
            def releaseRepoUrl = properties.getProperty('repository.release.url')
            def releaseRepoUsername = properties.getProperty('repository.release.username')
            def releaseRepoPassword = properties.getProperty('repository.release.password')
            def snapshotRepoUrl = properties.getProperty('repository.snapshot.url')
            def snapshotRepoUsername = properties.getProperty('repository.snapshot.username')
            def snapshotRepoPassword = properties.getProperty('repository.snapshot.password')
            url version.endsWith('SNAPSHOT') ? snapshotRepoUrl : releaseRepoUrl
            credentials {
                username version.endsWith('SNAPSHOT') ? snapshotRepoUsername : releaseRepoUsername
                password version.endsWith('SNAPSHOT') ? snapshotRepoPassword : releaseRepoPassword
            }
        }
    }
}
